# Copyright (C) 2013 Preferred Infrastructure and Nippon Telegraph and Telephone Corporation.

AC_INIT(mpsrc/wavy_kernel.h)
m4_include(m4/ax_check_link_flag.m4)
AC_CONFIG_AUX_DIR(ac)
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(jubatus_mpio, 0.4.2)
AC_CONFIG_HEADER(config.h)

# Default enabled -O4
AC_ARG_ENABLE(optimized, AS_HELP_STRING([--disable-optimized], [Compile with optimizations -O4 (default is enable)]))
# Check compiler driver can link -O4 generated file(for clang)
AS_IF([test "x$enable_optimized" != "xno"], [
	# Use default flag with -O3 -Wall
	save_c_flags="$CFLAGS"
	save_cxx_flags="$CXXFLAGS"
	CFLAGS="$CFLAGS -Wall"
	CXXFLAGS="$CXXFLAGS -Wall"
	AC_LANG_PUSH([C])
	AX_CHECK_LINK_FLAG(-O4, [CFLAGS="-O4 $CFLAGS"], [CFLAGS="$CFLAGS $save_c_flags"])
	AC_LANG_POP([C])
	AC_LANG_PUSH([C++])
	AX_CHECK_LINK_FLAG(-O4, [CXXFLAGS="-O4 $CXXFLAGS"], [CXXFLAGS="$CXXFLAGS $save_cxx_flags"])
	AC_LANG_POP([C++])
])

AC_PROG_CC
AC_PROG_CXX

AC_PROG_LIBTOOL
AM_PROG_AS
AM_PROG_CC_C_O


AC_CACHE_CHECK([for __sync_* atomic operations], kumofs_cv_atomic_ops, [
	AC_TRY_LINK([
		int atomic_sub(int i) { return __sync_sub_and_fetch(&i, 1); }
		int atomic_add(int i) { return __sync_add_and_fetch(&i, 1); }
		int atomic_cas(int i) { return __sync_bool_compare_and_swap(&i, 0, 1); }
	], [], kumofs_cv_atomic_ops="yes")
	])
if test "$kumofs_cv_atomic_ops" != "yes"; then
	AC_MSG_ERROR([__sync_* atomic operations are not supported.

Note that gcc < 4.1 is not supported.

If you are using gcc >= 4.1 and the default target CPU architecture is "i386", try to
add CFLAGS="-march=i686" and CXXFLAGS="-march=i686" options to ./configure as follows:

  $ ./configure CFLAGS="-march=i686" CXXFLAGS="-march=i686"
])
fi


AC_CHECK_LIB(stdc++, main)

AC_CHECK_LIB(pthread,pthread_create,,
	AC_MSG_ERROR([Can't find pthread library]))


case "$target_os" in
solaris*)
	AC_CHECK_LIB(socket,accept,,
		AC_MSG_ERROR([Can't find libsocket.]))
	AC_CHECK_LIB(nsl,inet_ntop,,
		AC_MSG_ERROR([Can't find libnsl.]))
	AC_CHECK_LIB(sendfile,sendfile,,
		AC_MSG_ERROR([Can't find libsendfile.]))
	CXXFLAGS="$CXXFLAGS -D_REENTRANT"
	CFLAGS="$CFLAGS -D_REENTRANT"
	;;

linux*)
	AC_MSG_CHECKING([if timerfd is enabled])
	AC_ARG_ENABLE(timerfd,
		AS_HELP_STRING([--disable-timerfd],
					   [use POSIX timer instead of timerfd. (compatility for linux < 2.6.25)]) )
	AC_MSG_RESULT($enable_timerfd)
	if test "$enable_timerfd" = "no"; then
		CXXFLAGS="$CXXFLAGS -DDISABLE_TIMERFD"
		CFLAGS="$CFLAGS -DDISABLE_TIMERFD"
		AC_CHECK_LIB(rt,timer_create,,
			AC_MSG_ERROR([Can't find rt library]))
	else
		AC_CHECK_HEADER(sys/timerfd.h, [],
						AC_MSG_ERROR([sys/timerfd.h is not available.

You can't use timerfd on this system.
It requires linux >= 2.6.25 and glibc >= 2.8.
Add --disable-timerfd option to use POSIX timer instead of timerfd.
]))
	fi

	AC_MSG_CHECKING([if signalfd is enabled])
	AC_ARG_ENABLE(signalfd,
		AS_HELP_STRING([--disable-signalfd],
					   [do not use signalfd. (compatility for linux < 2.6.22)]) )
	AC_MSG_RESULT($enable_signalfd)
	if test "$enable_signalfd" = "no"; then
		CXXFLAGS="$CXXFLAGS -DDISABLE_SIGNALFD"
		CFLAGS="$CFLAGS -DDISABLE_SIGNALFD"
	else
		AC_CHECK_HEADER(sys/signalfd.h, [],
						AC_MSG_ERROR([sys/signalfd.h is not available.

You can't use mp::wavy::add_signal on this system.
It requires linux >= 2.6.22 and glibc >= 2.8.
Add --disable-signalfd option to disable mp::wavy::add_signal.
]))
	fi

	;;
esac


AC_MSG_CHECKING([if debug option is enabled])
AC_ARG_ENABLE(debug,
	AS_HELP_STRING([--disable-debug],
				   [disable assert macros and omit -g option.]) )
if test "$enable_debug" != "no"; then
	CXXFLAGS="$CXXFLAGS -g"
	CFLAGS="$CFLAGS -g"
else
	CXXFLAGS="$CXXFLAGS -DNDEBUG"
	CFLAGS="$CFLAGS -DNDEBUG"
fi
AC_MSG_RESULT($enable_debug)

AC_OUTPUT([Makefile
		   jubatus/mp/Makefile
		   mpsrc/Makefile
		   test/Makefile])

